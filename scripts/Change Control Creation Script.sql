USE Aldebaran
GO

IF OBJECT_ID('PURCHASE_ORDER_NOTIFICATIONS', 'U') IS NOT NULL
	DROP TABLE PURCHASE_ORDER_NOTIFICATIONS
GO

IF OBJECT_ID('PURCHASE_ORDER_TRANSIT_ALARMS', 'U') IS NOT NULL
	DROP TABLE PURCHASE_ORDER_TRANSIT_ALARMS
GO

CREATE OR ALTER PROCEDURE SP_CUSTOMER_ORDERS_AFFECTED_BY_PURCHASE_ORDER_UPDATE
	@PURCHASEORDERID INT,
	@NEWEXPECTEDRECIPDATE DATE,
	@PURCHASEORDERDETAILQUANTITIES VARCHAR(MAX)
AS
BEGIN 

	/* RETURN DATA TABLE */
	DECLARE @CUSTOMERORDERNOTIFICATED TABLE
		(CUSTOMERORDERID INT NOT NULL PRIMARY KEY, 
		 ORDERNUMBER VARCHAR(10) NOT NULL,
		 CUSTOMERNAME VARCHAR(50), 
		 ORDERDATE DATE, 
		 ESTIMATEDDELIVERYDATE DATE,
		 STATUS_DOCUMENT_TYPE_NAME VARCHAR(30))
		 
	DECLARE @CUSTOMERORDERAFFECTED TABLE (CUSTOMERORDERID INT NOT NULL)

	/* GET THE ACTUAL EXPECTED_RECEIPT_DATE */
	DECLARE @EXPECTED_RECEIPT_DATE DATE 
	
	SELECT @EXPECTED_RECEIPT_DATE = EXPECTED_RECEIPT_DATE 
	  FROM PURCHASE_ORDERS 
	 WHERE PURCHASE_ORDER_ID = @PURCHASEORDERID

	/* SEND THE LIST OF UPDATED REFERENCES IN THE APPLICATION TO THE WORK TABLE */
	DECLARE @PURCHASEORDERDETAIL_PREV TABLE (IDREG INT NOT NULL IDENTITY(1,1), DETAIL VARCHAR(MAX))
	DECLARE @PURCHASEORDERDETAIL TABLE (REFERENCEID INT NOT NULL, QUANTITY INT)	
	DECLARE @REGISTER VARCHAR(MAX)
	DECLARE @REFERENCEID VARCHAR(10)
	DECLARE @QUANTITY VARCHAR(10)
	DECLARE @MAXREG INT = 0;
	DECLARE @IDREG INT = 0;

	INSERT INTO @PURCHASEORDERDETAIL_PREV (DETAIL)
		 SELECT VALUE FROM STRING_SPLIT(@PURCHASEORDERDETAILQUANTITIES,';')

	SELECT @MAXREG= MAX(IDREG), @IDREG = MIN(IDREG) FROM @PURCHASEORDERDETAIL_PREV
	
	WHILE @IDREG <= @MAXREG
	BEGIN
		SET @REGISTER = '';
		SET @REFERENCEID = 0;
		SET @QUANTITY = 0;

		SELECT @REGISTER = DETAIL 
		  FROM @PURCHASEORDERDETAIL_PREV 
		 WHERE IDREG = @IDREG


		 SET @REFERENCEID = SUBSTRING(@REGISTER, 0, CHARINDEX('-',@REGISTER))
		 SET @QUANTITY = SUBSTRING(@REGISTER, CHARINDEX('-',@REGISTER)+1, LEN(@REGISTER))

		 INSERT INTO @PURCHASEORDERDETAIL (REFERENCEID, QUANTITY)
			  SELECT @REFERENCEID, @QUANTITY

		SET @IDREG = @IDREG+1
	END 
	

	/* VARIABLES FOR VALIDATE NOTIFICATION RULES */
	DECLARE @EXPECTED_RECEIPT_DATE_CHANGED BIT = 0 
	DECLARE @DETAILS_DELETED BIT = 0 
	DECLARE @DETAILS_MODIFIED BIT = 0 

	/* VALIDATE IF THE ESTIMATED RECEIPT DATE CHANGES */		 
    IF @EXPECTED_RECEIPT_DATE < @NEWEXPECTEDRECIPDATE
		SET @EXPECTED_RECEIPT_DATE_CHANGED = 1 
		
	/* VALIDATE IF EXISTS DETAILS DELETED */
	SELECT @DETAILS_DELETED = CASE COUNT(1) WHEN 0 THEN 0 ELSE 1 END
	  FROM PURCHASE_ORDER_DETAILS A
     WHERE NOT EXISTS (SELECT 1 FROM @PURCHASEORDERDETAIL B WHERE B.REFERENCEID = A.REFERENCE_ID)
	 
	/* VALIDATE QUANTITIES CHANGED*/
	SELECT @DETAILS_MODIFIED = CASE COUNT(1) WHEN 0 THEN 0 ELSE 1 END
	  FROM PURCHASE_ORDER_DETAILS A
     WHERE EXISTS (SELECT 1 FROM @PURCHASEORDERDETAIL B WHERE B.REFERENCEID = A.REFERENCE_ID AND B.QUANTITY < A.REQUESTED_QUANTITY)

	/* IF I CHANGE THE ESTIMATED RECEIPT DATE, ORDERS WILL BE SEARCHED FOR ALL THE ITEMS OF THE PURCHASE ORDER */
	IF @EXPECTED_RECEIPT_DATE_CHANGED = 1
	BEGIN
		INSERT INTO @CUSTOMERORDERAFFECTED
			 SELECT DISTINCT A.CUSTOMER_ORDER_ID 
			   FROM CUSTOMER_ORDERS A
			   JOIN STATUS_DOCUMENT_TYPES B ON B.STATUS_DOCUMENT_TYPE_ID = A.STATUS_DOCUMENT_TYPE_ID
										   AND B.STATUS_ORDER IN (1,2,3)
			  WHERE A.ESTIMATED_DELIVERY_DATE BETWEEN @EXPECTED_RECEIPT_DATE AND @NEWEXPECTEDRECIPDATE
			    AND EXISTS (SELECT 1 
							  FROM CUSTOMER_ORDER_DETAILS C 
							  JOIN PURCHASE_ORDER_DETAILS D ON D.REFERENCE_ID = C.REFERENCE_ID
														   AND D.PURCHASE_ORDER_ID = @PURCHASEORDERID
							 WHERE C.CUSTOMER_ORDER_ID = A.CUSTOMER_ORDER_ID
							   AND (C.REQUESTED_QUANTITY-C.PROCESSED_QUANTITY-C.DELIVERED_QUANTITY) > 0)
	END

	IF @DETAILS_DELETED = 1
	BEGIN
		INSERT INTO @CUSTOMERORDERAFFECTED
			 SELECT DISTINCT A.CUSTOMER_ORDER_ID 
			   FROM CUSTOMER_ORDERS A
			   JOIN STATUS_DOCUMENT_TYPES B ON B.STATUS_DOCUMENT_TYPE_ID = A.STATUS_DOCUMENT_TYPE_ID
										   AND B.STATUS_ORDER IN (1,2,3)
			  WHERE A.ESTIMATED_DELIVERY_DATE > @NEWEXPECTEDRECIPDATE
			    AND EXISTS (SELECT 1 
							  FROM CUSTOMER_ORDER_DETAILS C 

							  JOIN PURCHASE_ORDER_DETAILS D ON D.REFERENCE_ID = C.REFERENCE_ID
														   AND D.PURCHASE_ORDER_ID = @PURCHASEORDERID
														   AND NOT EXISTS (SELECT 1 FROM @PURCHASEORDERDETAIL E WHERE E.REFERENCEID = D.REFERENCE_ID)
							 WHERE C.CUSTOMER_ORDER_ID = A.CUSTOMER_ORDER_ID
							   AND (C.REQUESTED_QUANTITY-C.PROCESSED_QUANTITY-C.DELIVERED_QUANTITY) > 0)
				AND NOT EXISTS (SELECT 1 FROM @CUSTOMERORDERAFFECTED F WHERE F.CUSTOMERORDERID = A.CUSTOMER_ORDER_ID)
	END

	IF @DETAILS_MODIFIED = 1
	BEGIN
		INSERT INTO @CUSTOMERORDERAFFECTED
			 SELECT DISTINCT A.CUSTOMER_ORDER_ID 
			   FROM CUSTOMER_ORDERS A
			   JOIN STATUS_DOCUMENT_TYPES B ON B.STATUS_DOCUMENT_TYPE_ID = A.STATUS_DOCUMENT_TYPE_ID
										   AND B.STATUS_ORDER IN (1,2,3)
			  WHERE A.ESTIMATED_DELIVERY_DATE > @NEWEXPECTEDRECIPDATE
			    AND EXISTS (SELECT 1 
							  FROM CUSTOMER_ORDER_DETAILS C 

							  JOIN PURCHASE_ORDER_DETAILS D ON D.REFERENCE_ID = C.REFERENCE_ID
														   AND D.PURCHASE_ORDER_ID = @PURCHASEORDERID
														   AND EXISTS (SELECT 1 FROM @PURCHASEORDERDETAIL E WHERE E.REFERENCEID = D.REFERENCE_ID AND E.QUANTITY < D.REQUESTED_QUANTITY)
							 WHERE C.CUSTOMER_ORDER_ID = A.CUSTOMER_ORDER_ID
							   AND (C.REQUESTED_QUANTITY-C.PROCESSED_QUANTITY-C.DELIVERED_QUANTITY) > 0)
				AND NOT EXISTS (SELECT 1 FROM @CUSTOMERORDERAFFECTED F WHERE F.CUSTOMERORDERID = A.CUSTOMER_ORDER_ID)
	END
	
	INSERT INTO @CUSTOMERORDERNOTIFICATED
		 SELECT A.CUSTOMER_ORDER_ID, A.ORDER_NUMBER, B.CUSTOMER_NAME, A.ORDER_DATE, A.ESTIMATED_DELIVERY_DATE, C.STATUS_DOCUMENT_TYPE_NAME 
		   FROM CUSTOMER_ORDERS A
		   JOIN CUSTOMERS B ON B.CUSTOMER_ID = A.CUSTOMER_ID
		   JOIN STATUS_DOCUMENT_TYPES C ON C.STATUS_DOCUMENT_TYPE_ID = A.STATUS_DOCUMENT_TYPE_ID
		  WHERE EXISTS (SELECT 1 FROM @CUSTOMERORDERAFFECTED TEMP WHERE TEMP.CUSTOMERORDERID = A.CUSTOMER_ORDER_ID )

	SELECT CUSTOMERORDERID, ORDERNUMBER, CUSTOMERNAME, ORDERDATE, ESTIMATEDDELIVERYDATE, STATUS_DOCUMENT_TYPE_NAME 
	  FROM @CUSTOMERORDERNOTIFICATED

END
GO

CREATE TABLE purchase_order_notifications
(
	PURCHASE_ORDER_NOTIFICATION_ID INT NOT NULL IDENTITY(1,1),
	MODIFIED_PURCHASE_ORDER_ID INT NOT NULL,
	NOTIFICATION_ID NVARCHAR(450) NOT NULL,
	NOTIFICATION_DATE DATETIME NOT NULL DEFAULT GETDATE(),
	NOTIFIED_MAIL_LIST VARCHAR(MAX) NOT NULL,
	CUSTOMER_ORDER_ID INT NOT NULL,
	NOTIFICATION_STATE SMALLINT NOT NULL,
	NOTIFICATION_SENDING_ERROR_MESSAGE VARCHAR(MAX),	
	CONSTRAINT FK_PURCHASE_ORDER_NOTIFICATIONS_CUSTOMER_ORDER FOREIGN KEY (CUSTOMER_ORDER_ID) REFERENCES CUSTOMER_ORDERS (CUSTOMER_ORDER_ID),	
	CONSTRAINT FK_PURCHASE_ORDER_NOTIFICATIONS_MODIFIED_PURCHASE_ORDER FOREIGN KEY (MODIFIED_PURCHASE_ORDER_ID) REFERENCES MODIFIED_PURCHASE_ORDERS (MODIFIED_PURCHASE_ORDER_ID),
	CONSTRAINT PK_PURCHASE_ORDER_NOTIFICATIONS PRIMARY KEY(PURCHASE_ORDER_NOTIFICATION_ID)
)
GO

CREATE TABLE purchase_order_transit_alarms
(
	PURCHASE_ORDER_TRANSIT_ALARM_ID INT NOT NULL IDENTITY(1,1),
	MODIFIED_PURCHASE_ORDER_ID INT NOT NULL,
	OLD_EXPECTED_RECEIPT_DATE DATE NOT NULL,
	CONSTRAINT PK_PURCHASE_ORDER_TRANSIT_ALARM PRIMARY KEY (PURCHASE_ORDER_TRANSIT_ALARM_ID),
	CONSTRAINT FK_PURCHASE_ORDER_TRANSIT_ALARM_MODIFIED_PURCHASE_ORDER FOREIGN KEY (MODIFIED_PURCHASE_ORDER_ID) REFERENCES MODIFIED_PURCHASE_ORDERS (MODIFIED_PURCHASE_ORDER_ID)
)
GO

CREATE TABLE visualized_purchase_order_transit_alarms
(
	PURCHASE_ORDER_TRANSIT_ALARM_ID INT NOT NULL,
	EMPLOYEE_ID INT NOT NULL,
	VISUALIZED_DATE DATETIME NOT NULL CONSTRAINT DF_VISUALIZED_PURCHASE_ORDER_TRANSIT_ALARMS_VISUALIZED_DATE DEFAULT GETDATE(),
	CONSTRAINT PK_VISUALIZED_PURCHASE_ORDER_TRANSIT_ALARM PRIMARY KEY (PURCHASE_ORDER_TRANSIT_ALARM_ID, EMPLOYEE_ID),
	CONSTRAINT FK_VISUALIZED_PURCHASE_ORDER_TRANSIT_ALARM_PURCHASE_ORDER_TRANSIT_ALARM FOREIGN KEY (PURCHASE_ORDER_TRANSIT_ALARM_ID) REFERENCES PURCHASE_ORDER_TRANSIT_ALARMS (PURCHASE_ORDER_TRANSIT_ALARM_ID),
	CONSTRAINT FK_VISUALIZED_PURCHASE_ORDER_TRANSIT_ALARM_EMPLOYEE FOREIGN KEY (EMPLOYEE_ID) REFERENCES EMPLOYEES (EMPLOYEE_ID)
)
GO

ALTER TABLE [customers] DROP COLUMN EMAIL2
GO
ALTER TABLE [customers] DROP COLUMN EMAIL3
GO

EXEC sp_rename 'customers.EMAIL1',  'EMAIL', 'COLUMN';
GO

ALTER TABLE [customers] ALTER COLUMN EMAIL NVARCHAR(MAX) NOT NULL
GO

CREATE OR ALTER PROCEDURE SP_CUSTOMER_ORDERS_FOR_NOTIFICATION_BY_MODIFIED_PURCHASE_ORDER_ID
	@MODIFIED_PURCHASE_ORDER_ID INT
AS
BEGIN 

	SELECT A.PURCHASE_ORDER_ID, B.MODIFIED_PURCHASE_ORDER_ID, B.NOTIFIED_MAIL_LIST, C.MODIFICATION_REASON_NAME, D.ORDER_NUMBER, E.CUSTOMER_NAME
	  FROM MODIFIED_PURCHASE_ORDERS A
	  JOIN PURCHASE_ORDER_NOTIFICATIONS B ON B.MODIFIED_PURCHASE_ORDER_ID = A.MODIFIED_PURCHASE_ORDER_ID
	  JOIN MODIFICATION_REASONS C ON C.MODIFICATION_REASON_ID = A.MODIFICATION_REASON_ID
	  JOIN CUSTOMER_ORDERS D ON D.CUSTOMER_ORDER_ID = B.CUSTOMER_ORDER_ID
	  JOIN CUSTOMERS E ON E.CUSTOMER_ID = D.CUSTOMER_ID
	 WHERE B.NOTIFICATION_STATE = 0 
	   AND A.MODIFIED_PURCHASE_ORDER_ID = @MODIFIED_PURCHASE_ORDER_ID
	   
END
GO

CREATE OR ALTER PROCEDURE SP_CUSTOMER_ORDERS_POSSIBLY_AFFECTED_BY_PURCHASE_ORDER_ID
	@PURCHASEORDERID INT
AS
BEGIN 

	/* RETURN DATA TABLE */
	DECLARE @CUSTOMERORDERNOTIFICATED TABLE
		(CUSTOMERORDERID INT NOT NULL PRIMARY KEY, 
		 ORDERNUMBER VARCHAR(10) NOT NULL,
		 CUSTOMERNAME VARCHAR(50), 
		 ORDERDATE DATE, 
		 ESTIMATEDDELIVERYDATE DATE,
		 STATUS_DOCUMENT_TYPE_NAME VARCHAR(30))
		 
	DECLARE @CUSTOMERORDERAFFECTED TABLE (CUSTOMERORDERID INT NOT NULL)

	/* GET THE ACTUAL EXPECTED_RECEIPT_DATE */
	DECLARE @EXPECTED_RECEIPT_DATE DATE 
	
	SELECT @EXPECTED_RECEIPT_DATE = EXPECTED_RECEIPT_DATE 
	  FROM PURCHASE_ORDERS 
	 WHERE PURCHASE_ORDER_ID = @PURCHASEORDERID
	
	INSERT INTO @CUSTOMERORDERAFFECTED
		 SELECT DISTINCT A.CUSTOMER_ORDER_ID 
		   FROM CUSTOMER_ORDERS A
		   JOIN STATUS_DOCUMENT_TYPES B ON B.STATUS_DOCUMENT_TYPE_ID = A.STATUS_DOCUMENT_TYPE_ID
									   AND B.STATUS_ORDER IN (1,2,3)
		  WHERE A.ESTIMATED_DELIVERY_DATE >= @EXPECTED_RECEIPT_DATE 
		    AND EXISTS (SELECT 1 
			  	  	  	  FROM CUSTOMER_ORDER_DETAILS C 
						  JOIN PURCHASE_ORDER_DETAILS D ON D.REFERENCE_ID = C.REFERENCE_ID
													   AND D.PURCHASE_ORDER_ID = @PURCHASEORDERID
						 WHERE C.CUSTOMER_ORDER_ID = A.CUSTOMER_ORDER_ID
						   AND (C.REQUESTED_QUANTITY-C.PROCESSED_QUANTITY-C.DELIVERED_QUANTITY) > 0)
	
	INSERT INTO @CUSTOMERORDERNOTIFICATED
		 SELECT A.CUSTOMER_ORDER_ID, A.ORDER_NUMBER, B.CUSTOMER_NAME, A.ORDER_DATE, A.ESTIMATED_DELIVERY_DATE, C.STATUS_DOCUMENT_TYPE_NAME 
		   FROM CUSTOMER_ORDERS A
		   JOIN CUSTOMERS B ON B.CUSTOMER_ID = A.CUSTOMER_ID
		   JOIN STATUS_DOCUMENT_TYPES C ON C.STATUS_DOCUMENT_TYPE_ID = A.STATUS_DOCUMENT_TYPE_ID
		  WHERE EXISTS (SELECT 1 FROM @CUSTOMERORDERAFFECTED TEMP WHERE TEMP.CUSTOMERORDERID = A.CUSTOMER_ORDER_ID )

	SELECT CUSTOMERORDERID, ORDERNUMBER, CUSTOMERNAME, ORDERDATE, ESTIMATEDDELIVERYDATE, STATUS_DOCUMENT_TYPE_NAME 
	  FROM @CUSTOMERORDERNOTIFICATED

END
GO

SET IDENTITY_INSERT [dbo].[notification_templates] ON 
INSERT [dbo].[notification_templates] ([NOTIFICATION_TEMPLATE_ID], [NAME], [SUBJECT], [MESSAGE]) VALUES (9, N'PurchaseOrder:Update:Customer:Order', N'Afectación del pedido por modificación de la orden de compra', N'<p>Lamentamos informarle que su pedido ha sido afectado por una actualización de la orden de compra que incluía los artículos solicitados por usted. Entendemos que esto puede causarle inconvenientes y le pedimos disculpas por cualquier molestia que esto pueda ocasionarle. Si tiene alguna pregunta o necesita asistencia adicional, no dude en ponerse en contacto con nuestro equipo de atención al cliente.</p>')
SET IDENTITY_INSERT [dbo].[notification_templates] OFF
GO

